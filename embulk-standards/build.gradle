// TODO: Have more details in description and summary.
description = 'Embulk standard plugins'
ext {
    summary = 'Embulk standard plugins'
}

repositories {
    mavenCentral()
}

configurations {
    jruby
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

dependencies {
    compileOnly project(":embulk-api")
    compileOnly project(":embulk-spi")
    compileOnly project(":embulk-core")

    compile project(":embulk-decoder-bzip2")
    compile project(":embulk-decoder-gzip")
    compile project(":embulk-encoder-bzip2")
    compile project(":embulk-encoder-gzip")
    compile project(":embulk-guess-bzip2")
    compile project(":embulk-guess-csv")
    compile project(":embulk-guess-csv_all_strings")
    compile project(":embulk-guess-gzip")
    compile project(":embulk-guess-json")
    compile project(":embulk-filter-remove_columns")
    compile project(":embulk-filter-rename")
    compile project(":embulk-formatter-csv")
    compile project(":embulk-input-config")
    compile project(":embulk-input-file")
    compile project(":embulk-output-file")
    compile project(":embulk-output-null")
    compile project(":embulk-output-stdout")
    compile project(":embulk-parser-csv")
    compile project(":embulk-parser-json")

    testImplementation "junit:junit:4.12"
    testImplementation "org.hamcrest:hamcrest-library:1.3"
    testImplementation project(":embulk-api")
    testImplementation project(":embulk-spi")
    testImplementation project(":embulk-core")
    testImplementation project(":embulk-core").sourceSets.test.output
    testImplementation project(":embulk-junit4")
    testImplementation "ch.qos.logback:logback-classic:1.1.3"

    testImplementation "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    testImplementation "com.google.guava:guava:18.0"
    testImplementation "joda-time:joda-time:2.9.2"
    testImplementation "org.msgpack:msgpack-core:0.8.11"

    // TODO: Remove this, and load it with the proper DependencyClassLoader.
    // This statement gets it loaded by the top-level ClassLoader.
    testImplementation project(":embulk-deps")

    testImplementation "org.jruby:jruby-complete:9.1.15.0"

    jruby "org.jruby:jruby-complete:9.1.15.0"
}

// Tests depending on ":embulk-ruby" to run Ruby-based guess mechanisms.
// TODO: Remove them once guess implementations are converted to Java.

evaluationDependsOn(":embulk-ruby")

task cleanDependencyGems {
    doFirst {
        delete("${buildDir}/dependencyGems")
        mkdir("${buildDir}/dependencyGems")
    }
}

task installDependencyGems(type: JavaExec, dependsOn: "cleanDependencyGems") {
    classpath = configurations.jruby
    main = "org.jruby.Main"
    args = ["-rjars/setup", "-S", "gem", "install", "msgpack:1.1.0"]
    environment "GEM_HOME": "${buildDir}/dependencyGems"
}

task installEmbulkGem(type: JavaExec, dependsOn: ["installDependencyGems", ":embulk-ruby:gem"]) {
    doFirst {
        println tasks.getByPath(":embulk-ruby:gem").archivePath
    }
    classpath = configurations.jruby
    main = "org.jruby.Main"
    args = ["-rjars/setup", "-S", "gem", "install", "--local", tasks.getByPath(":embulk-ruby:gem").archivePath]
    environment "GEM_HOME": "${buildDir}/dependencyGems"
}

test.dependsOn("installEmbulkGem")
test {
    environment "GEM_HOME", "${buildDir}/dependencyGems"
}
